# We have a conf and classes directory, add to BBPATH. Add it at the
# front because we want to override files like fs-perms.txt.
BBPATH =. "${LAYERDIR}:"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
	${LAYERDIR}/recipes-*/*/*.bbappend \
        ${LAYERDIR}/fixes/*/recipes-*/*/*.bb \
        ${LAYERDIR}/fixes/*/recipes-*/*/*.bbappend \
        ${LAYERDIR}/fixes/*/recipes-*/*/*/*.bb \
        ${LAYERDIR}/fixes/*/recipes-*/*/*/*.bbappend \
"

# Set a variable to get to the top of the metadata location
IMAGE_DSK_BASE := '${LAYERDIR}'

# Set a variable to get to the top of the Reference OS for IOT metadata location.
META_REF_OS_BASE := '${LAYERDIR}'

# Used by image-dsk.bbclass to find lib/image-dsk.py.
IMAGE_DSK_BASE ?= '${META_REF_OS_BASE}'

# Required by stateless.bbclass to find patches needed only when
# compiling stateless.
STATELESS_PATCHES_BASE ?= "${META_REF_OS_BASE}/conf/distro/stateless-patches"

BBFILE_COLLECTIONS += "ref-os-iot"
BBFILE_PATTERN_ref-os-iot := "^${LAYERDIR}/"
BBFILE_PRIORITY_ref-os-iot = "7"

# This and the corresponding CONF_VERSION value in local.conf.sample
# need to be increased by one each time a change is made to
# local.conf.sample that requires manually updating a local.conf after
# updating the meta data.
#
# A separate, Reference OS for IOT specific variable is used to make the value
# available to derived distros.
REFKIT_LOCALCONF_VERSION = "2"
LOCALCONF_VERSION = "${REFKIT_LOCALCONF_VERSION}"

# Same for LCONF_VERSION in bblayer.conf.sample.
REFKIT_LAYER_CONF_VERSION = "2"
LAYER_CONF_VERSION = "${REFKIT_LAYER_CONF_VERSION}"

# The default error messages use shell meta* wildcards to find the
# conf files which in refkit happen to match two files (one from
# meta and the real one from meta-refkit). Be more specific to avoid
# user confusion.
SANITY_LOCALCONF_SAMPLE = "${META_REF_OS_BASE}/conf/local.conf.sample"
SANITY_BBLAYERCONF_SAMPLE = "${META_REF_OS_BASE}/conf/bblayers.conf.sample"
