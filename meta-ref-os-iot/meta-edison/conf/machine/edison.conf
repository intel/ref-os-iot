#@TYPE: Machine
#@NAME: edison
#@DESCRIPTION: Machine configuration for edison systems

include conf/machine/include/tune-atom.inc

MACHINE_FEATURES = "bluetooth alsa pci serial usbgadget usbhost wifi x86 ext3 systemd"

IMAGE_CLASSES = "image_types_edison"
IMAGE_FSTYPES = " tar.bz2 ext4 toflash"
REFKIT_USE_DSK_IMAGES = "0"
IMAGE_ROOTFS_SIZE = "524288"

# Bootloader config

PREFERRED_PROVIDER_virtual/bootloader ?=  "u-boot"
PREFERRED_VERSION_u-boot ?= "2015.10"
PREFERRED_VERSION_u-boot-fw-utils ?= "2015.10"
UBOOT_MACHINE = "edison_config"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "u-boot u-boot-fw-utils kernel-modules sst-fw mcu-fw-bin mcu-fw-load"

# Kernel config

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto-edison"
PREFERRED_VERSION_linux-yocto-edison ?= "3.10.98"
KERNEL_IMAGETYPE = "bzImage"
SERIAL_CONSOLES ?= "115200;ttyMFD2"

KERNEL_MODULE_AUTOLOAD += "bcm_bt_lpm"
KERNEL_MODULE_AUTOLOAD += "bcm4334x"
module_conf_bcm4334x = "options bcm4334x firmware_path=/etc/firmware/fw_bcmdhd.bin nvram_path=/etc/firmware/bcmdhd.cal op_mode=4"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += "kernel-module-g-multi"
KERNEL_MODULE_AUTOLOAD  += "g_multi"
module_conf_g_multi = "options g_multi file=/dev/mmcblk0p8 ro=1 stall=0 idVendor=0x8087 idProduct=0x0A9E iProduct=Edison iManufacturer=Intel"

KERNEL_MODULE_PROBECONF += "bcm4334x g_multi"

XSERVER = "xserver-xorg \
           xf86-input-keyboard \
           xf86-input-evdev \
           xf86-video-dummy"

MACHINE_EXTRA_RDEPENDS = " \
    resize-rootfs \
    bcm43340-fw \
    bcm43340-bt \
    bcm43340-mod \
    bluetooth-rfkill-event \
"

MACHINE_EXTRA_RRECOMMENDS = " \
    ap-mode-toggle \
    battery-voltage \
    watchdog-sample \
    post-install \
    zeromq-dev \
    packagegroup-core-buildessential \
    cppzmq-dev \
"
# in Edison rfkill and connman together breaks bluetooth
# remove the dependency of connectivity package in edison
RDEPENDS_packagegroup-core-connectivity_remove = "connman"
RDEPENDS_packagegroup-tools-interactive_remove = "connman-client"
